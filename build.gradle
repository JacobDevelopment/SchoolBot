plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'nu.studer.jooq' version '6.0.1'
}

application {
    mainClassName = 'io.jking.tickster.Starter'
}

repositories {
    mavenCentral()
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.3.0'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '5.0.0'
    implementation group: 'org.jooq', name: 'jooq', version: '3.15.4'
    jooqGenerator group: 'org.postgresql', name: 'postgresql', version: '42.3.0'
    implementation group: 'net.jodah', name: 'expiringmap', version: '0.5.10'

    implementation ('com.github.DV8FromTheWorld:JDA:rework~interactions-SNAPSHOT') {
        exclude group: 'club.minnced', module: 'opus-java'
    }

    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha10'
}

jooq {
    version = '3.15.4'
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5432/postgres'
                    user = 'postgres'
                    password = 'password'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = '.*'
                        excludes = ''
                    }
                    generate {
                        records = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'io.jking.tickster.jooq'
                        directory = "src/main/jooq"
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

group = 'io.jking'
description = 'Untitled'
compileJava.options.encoding = 'UTF-8'